
-- @path OCL=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path OCLt=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path ACT=/beautyocl.actions/model/actions.ecore
--
-- if condition then 
--   true
-- else
--   false
-- endif        ==> condition 
--
module "if_true_false";
create OUT : ACT, OUT2 : OCLt from IN : OCL; 

helper context OCL!BooleanExp    def: isBooleanExpr(b : Boolean) : Boolean = self.booleanSymbol = b;
helper context OCL!OclExpression def: isBooleanExpr(b : Boolean) : Boolean = false;

rule replaceByCondition {
	from o : OCL!IfExp (
		o.thenExpression.isBooleanExpr(true) and o.elseExpression.isBooleanExpr(false)
	)
	to a : ACT!DeleteMoveChildren
	do {
		a.source <- o;
		a.children <- o.condition;
	}
}

rule replaceByNegatedCondition {
	from o : OCL!IfExp (
		o.thenExpression.isBooleanExpr(false) and o.elseExpression.isBooleanExpr(true)
	)
	to a : ACT!Replace, r : OCLt!OperatorCallExp (
			name <- 'not'
	   )
	do {
		a.source <- o;
		r.source <- o.condition;
		a.target <- o.condition;
	}
}
