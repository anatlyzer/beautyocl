-- @path OCL=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path OCLt=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path ACT=/beautyocl.actions/model/actions.ecore

module "remove_iterator";
create OUT : ACT, OUT2 : OCLt from IN : OCL; 

helper context OCL!BooleanExp    def: isBooleanExpr(b : Boolean) : Boolean = self.booleanSymbol = b;
helper context OCL!OclExpression def: isBooleanExpr(b : Boolean) : Boolean = false;


rule trueSelect {
	from it : OCL!IteratorExp (
		it.name = 'select' and it.body.isBooleanExpr(true)
	)
	to a : ACT!DeleteMoveChildren
	do {
		a.source <- it;
		a.children <- it.source;
	}
}

-- aCol->collect(c | c)  => aCol
rule unnededCollect {
	from it : OCL!IteratorExp (
		if it.name = 'collect' and it.body.oclIsKindOf(OCL!VariableExp) then
			it.body.varDcl = it.iteratorVar
		else
			false
		endif
	)
	to a : ACT!DeleteMoveChildren
	do {
		a.source <- it;
		a.children <- it.source;
	}
}

-- aCol->forAll(c | true)  => true
-- aCol->forAll(c | false)  => false
-- aCol->exists(c | true)  => true
-- aCol->exists(c | false)  => false
rule simplifyForAllExists {
	from it : OCL!IteratorExp (
		(it.name = 'forAll' or it.name = 'exists') and it.body.oclIsKindOf(OCL!BooleanExp)
	)
	to a : ACT!Replace,
	   r : OCLt!BooleanExp (
			booleanSymbol <- it.body.booleanSymbol
	   )
	do {
		a.source <- it;
		a.target <- r;
	}
}

