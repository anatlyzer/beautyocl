-- @path OCL=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path OCLt=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path ACT=/beautyocl.actions/model/actions.ecore
--
-- Apply identity elements to several categories, booleans, integers
-- https://en.wikipedia.org/wiki/Identity_element
--
module "identityelement";
create OUT : ACT, OUT2 : OCLt from IN : OCL; 


helper context OCL!BooleanExp    def: isBooleanExpr(b : Boolean) : Boolean = self.booleanSymbol = b;
helper context OCL!OclExpression def: isBooleanExpr(b : Boolean) : Boolean = false;


--
-- expr or false => expr 
--
-- @ignore invalid-assignment-imperative-binding
rule or_false {
	from o : OCL!BinaryOperatorCallExp (
		o.name = 'or' and
		(o.source.isBooleanExpr(false) or o.argument.isBooleanExpr(false))
	)
	to a : ACT!DeleteMoveChildren
	do {
		a.source <- o;
		a.children <- if o.source.isBooleanExpr(false) then o.argument else o.source endif;
	}
}


--
-- expr and true => expr 
--
-- @ignore invalid-assignment-imperative-binding
rule and_true {
	from o : OCL!BinaryOperatorCallExp (
		o.name = 'and' and
		(o.source.isBooleanExpr(true) or o.argument.isBooleanExpr(true))
	)
	to a : ACT!DeleteMoveChildren
	do {
		a.source <- o;
		a.children <- if o.source.isBooleanExpr(true) then o.argument else o.source endif;
	}
}

