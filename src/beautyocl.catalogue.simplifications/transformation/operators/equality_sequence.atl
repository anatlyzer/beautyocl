
-- @path OCL=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path OCLt=/beautyocl.catalogue.simplifications/metamodels/superimposed.ecore
-- @path ACT=/beautyocl.actions/model/actions.ecore
-- @path COMP=/beautyocl.catalogue.simplifications/metamodels/comparison.ecore

--
-- Given a sequence of or operations like:
--  
--    exp = value1 or exp = value1 or exp.value2
--
-- We generate:
--
--    Set { value1, value2, value3 }->includes(exp)
--

module "equality_sequence";
create OUT : ACT, OUT2 : OCLt from IN : OCL, IN3 : COMP; 

helper def : comp : COMP!ExprComparator = COMP!ExprComparator.allInstances()->first();

helper def : MIN_SIZE : Integer = 4;


helper context OCL!OclExpression   def: isEquality(eqs : Set(String)) : Boolean =  false;
helper context OCL!BinaryOperatorCallExp def: isEquality(eqs : Set(String)) : Boolean =  eqs->includes(self.name);

-- The precondition is that it is an equality (checked with isEquality)
-- Returns the values for which the key are the same
helper context OCL!BinaryOperatorCallExp def: isSame(exp : OCL!OclExpression) : OCL!OclExpression =  
	let left  : OCL!OclExpression = self.source in
	let right : OCL!OclExpression = self.argument in
		if thisModule.comp.isSameExpr(exp, left) then
			right
		else if thisModule.comp.isSameExpr(exp, right) then
			left
		else OclUndefined endif endif;

-- @force-declared-return-type
helper context OCL!OclExpression def: operatorSequence(operator : String, eqs : Set(String)) : Sequence(OCL!BinaryOperatorCallExp) = 
	if self.isEquality(eqs) then
		Sequence { self }
	else
		Sequence { }
	endif;

-- @force-declared-return-type
helper context OCL!BinaryOperatorCallExp def: operatorSequence(operator : String, eqs : Set(String)) : Sequence(OCL!BinaryOperatorCallExp) = 
	if self.name = operator and self.argument.isEquality(eqs) then
		Sequence { self.argument }->union(self.source.operatorSequence(operator, eqs))
	else
		if self.isEquality(eqs) then
			Sequence { self } 
		else		
			Sequence { }
		endif
	endif;


-- @force-declared-return-type
helper context OCL!OclExpression def: operatorSequenceIndependentArgs(operator : String, eqs : Set(String)) : Sequence(OCL!OclExpression) = 
	if self.isEquality(eqs) then
		Sequence { }
	else
		Sequence { self }
	endif;

-- @force-declared-return-type
helper context OCL!BinaryOperatorCallExp def: operatorSequenceIndependentArgs(operator : String, eqs : Set(String)) : Sequence(OCL!OclExpression) = 
	if self.name = operator and self.argument.isEquality(eqs) then
		self.source.operatorSequenceIndependentArgs(operator, eqs)
	else
		if self.isEquality(eqs) then
			Sequence{ }
		else
			Sequence { self }
		endif
	endif;
	
helper context OCL!BinaryOperatorCallExp def: operatorSequenceIfSame(operator : String, eqs : Set(String), min_size : Integer) : TupleType(value : OCL!OclExpression, keys : Sequence(OCL!OclExpression), independent : Sequence(OCL!OclExpression)) = 
	let seq : Sequence(OCL!BinaryOperatorCallExp) = self.operatorSequence(operator, eqs) in
		if seq->size() >= min_size then
			-- seq has equalities x = y
			let left  : OCL!OclExpression = seq->first().source in
			let right : OCL!OclExpression = seq->first().argument in
			let samesLeft  : Sequence(OCL!OclExpression) = seq->collect(e | e.isSame(left))->excluding(OclUndefined) in
			let samesRight : Sequence(OCL!OclExpression) = seq->collect(e | e.isSame(right))->excluding(OclUndefined) in
			if samesLeft->notEmpty() and samesLeft->size() = seq->size() then
				Tuple { value = left, keys = samesLeft, independent = self.operatorSequenceIndependentArgs(operator, eqs) }
			else if samesRight->notEmpty() and samesRight->size() = seq->size() then
				Tuple { value = right, keys = samesRight, independent = self.operatorSequenceIndependentArgs(operator, eqs) }
			else OclUndefined endif endif
		else
			OclUndefined
		endif;
	


-- exp = value1 or exp = value1 or exp = value2
-- or:
--   argument: exp = value2
--     source: or
--             argument: exp = value1
--             source: exp = value1 

-- @ignore invalid-assignment-imperative-binding
rule fill(main : OCLt!BinaryOperatorCallExp, list : Sequence(OCL!OclExpression), operator : String, input : OCL!BinaryOperatorCallExp) {
	-- argument of main is already set by the caller
	to 
		c : ACT!CompositeAction,
		binary : OCLt!BinaryOperatorCallExp (
			name <- operator
		),
		set_argument : ACT!SetP (
			propertyName <- 'arguments' 
		),
		set_source : ACT!SetP (
			propertyName <- 'source' 
		)		
	do {
		c.source <- input;
		
		if ( list->size() = 1 ) {
			-- main.source <- list->first();
			set_source.source <- main;
			set_source.value <- list->first();					

			c.actions <- Sequence { set_source };
		} else {
			-- main.source <- binary;			
			set_source.source <- main;
			set_source.value <- binary;
			
			-- binary.argument <- list->first();
			set_argument.source <- binary;
			set_argument.value <- list->first();
		
			
			c.actions <- Sequence { set_argument, set_source,
						thisModule.fill(binary, list->subSequence(2, list->size()), operator, input) };
		}
		
		c;
	}
}

-- @ignore invalid-assignment-imperative-binding
rule find_sequence_of_operations_or {
	from o : OCL!BinaryOperatorCallExp (
		let seq : TupleType(value : OCL!OclExpression, keys : Sequence(OCL!OclExpression), independent : Sequence(OCL!OclExpression)) = o.operatorSequenceIfSame('or', Set {'='}, thisModule.MIN_SIZE) 
		 in seq <> OclUndefined 
		 -- TODO: Check with refImmediateComposite if this is the larger one
	)
    using {
		seq : TupleType(value : OCL!OclExpression, keys : Sequence(OCL!OclExpression), independent : Sequence(OCL!OclExpression)) = 
			o.operatorSequenceIfSame('or', Set {'='}, thisModule.MIN_SIZE);	
    }
	to c : ACT!CompositeAction (
		actions <- Sequence { set_elements, set_includes_source, set_includes_arguments }->
			union(if seq.independent->isEmpty() then Sequence { } else Sequence { set_argument_additional } endif)->
			including(a)	
	),
	a : ACT!Replace, 
	includesOp : OCLt!CollectionOperationCallExp (
		name <- 'includes'
	),
	additional : OCLt!BinaryOperatorCallExp (
		name <- 'or'
	),
	set_argument_additional  : ACT!SetP,
	
	set_exp : OCLt!SetExp,
	set_elements  : ACT!SetP,
	set_includes_source    : ACT!SetP,
	set_includes_arguments : ACT!SetP
	do {
		c.source <- o;
		
		a.source <- o;
		if ( seq.independent->isEmpty() ) {
			a.target <- includesOp;
		} else {
			a.target <- additional;
		}
	
		if ( seq.independent->notEmpty() ) {
			c.actions <- c.actions->prepend(thisModule.fill(additional, seq.independent, 'or', o));
			set_argument_additional.propertyName <- 'arguments'; -- Probably argument
			set_argument_additional.source <- additional;
			set_argument_additional.value <- includesOp;			
		}
		
		set_includes_source.propertyName <- 'source';
		set_includes_source.source <- includesOp;
		set_includes_source.value <- set_exp;
		
		set_includes_arguments.propertyName <- 'arguments';
		set_includes_arguments.source <- includesOp;
		set_includes_arguments.value <- seq.value;
		
		set_elements.propertyName <- 'elements';
		set_elements.source <- set_exp;
		set_elements.value <- seq.keys;
	}
}

-- @ignore invalid-assignment-imperative-binding
rule find_sequence_of_operations_and{
	from o : OCL!BinaryOperatorCallExp (
		let seq : TupleType(value : OCL!OclExpression, keys : Sequence(OCL!OclExpression)) = o.operatorSequenceIfSame('and', Set {'='}, 2) 
		 in if seq <> OclUndefined then
		 		seq.keys->forAll(k | k.oclIsKindOf(OCL!StringExp) or k.oclIsKindOf(OCL!IntegerExp))
			else
				false
			endif
		 -- TODO: Check with refImmediateComposite if this is the larger one
	)
	to a : ACT!Replace,
	true_value : OCLt!BooleanExp (
		booleanSymbol <- false
	)
	do {
		a.source <- o;
		a.target <- true_value;
	}
}

